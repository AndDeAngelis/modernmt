package eu.modernmt.processing.tokenizer.jflex.annotators;

import eu.modernmt.processing.tokenizer.jflex.JFlexTokenAnnotator;

%%

//pyflex class JFlexTokenAnnotator

/**
 *  Source: https://unicode-table.com/
 *
 *  1100-11FF: Hangul Jamo (korean)
 *
 *  2E80-2EFF: CJK Radicals Supplement (chinese, japanese, vietnamese, korean)
 *  2F00-2FDF: Kangxi Radicals (chinese)
 *  2FE0-2FEF: ---
 *  2FF0-2FFF: Ideographic Description Characters
 *  3000-303F: CJK Symbols and Punctuation
 *  3040-309F: Hiragana (japanese)
 *  30A0-30FF: Katakana (japanese)
 *  3100-312F: Bopomofo (chinese)
 *  3130-318F: Hangul Compatibility Jamo (chinese)
 *  3190-319F: Kanbun (chinese)
 *  31A0-31BF: Bopomofo Extended
 *  31C0-31EF: CJK Strokes (chinese)
 *  31F0-31FF: Katakana Phonetic Extensions (japanese)
 *  3200-32FF: Enclosed CJK Letters and Months (chinese)
 *  3300-33FF: CJK Compatibility (chinese, japanese, korean, vietnamese)
 *  3400-4DBF: CJK Unified Ideographs Extension A (chinese, japanese, korean, vietnamese)
 *  4DC0-4DFF: Yijing Hexagram Symbols
 *  4E00-9FFF: CJK Unified Ideographs (chinese, japanese, korean, vietnamese)
 *  A000-A48F: Yi Syllables (yi)
 *  A490-A4CF: Yi Radicals (yi)
 *
 *  A840-A87F: Phags-pa (mongolian, sanskrit, tibetan, chinese, uyghur)
 *
 *  AC00-D7AF: Hangul Syllables (korean)
 *  D7B0-D7FF: Hangul Jamo Extended-B
 *  D800-DB7F: High Surrogates (approximation: surrogates are considered CJKV)
 *  DB80-DBFF: High Private Use Surrogates (approximation: surrogates are considered CJKV)
 *  DC00-DFFF: Low Surrogates (approximation: surrogates are considered CJKV)
 *
 *  F900-FAFF: CJK Compatibility Ideographs
 *
 *  FE30-FE4F: CJK Compatibility Forms
 *
 *  FF65-FFDC: Halfwidth and Fullwidth Forms
 */
CJKV       = [\u1100-\u11FF\u2E80-\uA4CF\uA840-\uA87F\uAC00-\uDFFF\uF900-\uFAFF\uFE30-\uFE4F\uFF65-\uFFDC]
_          = " "
Letter     = !(![:letter:]|{CJKV})

UrlPart    = [-a-zA-Z0-9_\+~#]+
Number     = ((\-|\+)?[0-9]+)?([\.,][0-9]+)*

%%

/* Default Ignore */
.                                                                                                                       { /* ignore */ }

/* Number */
{Number}                                                                                                                { return protect(1, 0); }
/* Percentage */
{Number}\%                                                                                                              { return protect(1, 0); }
/* Temperature */
{Number}(°F|°C|°K)                                                                                                      { return protect(1, 0); }

/* Dates */
[0-9]{1,4}(\/[0-9]{1,4}){0,2}                                                                                           { return protect(1, 0); }

/* URL */
((https?|HTTPS?)\:\/\/)?({UrlPart}\.)+{UrlPart}\/?({UrlPart}\/?)*(\.{Letter}+)?(\?({UrlPart}(\={UrlPart})?\&?)*)?       { return protect(1, 0); }

/* E-mail */
([a-z0-9_\.-]+)@([\da-z\.-]+)\.([a-z\.]{2,6})                                                                           { return protect(1, 0); }

/* Acronym */
{Letter}\.({Letter}\.)+                                                                                                 { return protect(1, 0); }

/* &-separated entities */
{Letter}\&{Letter}                                                                                                      { return protect(1, 0); }

/* Pseudo HTML Entities */
\&#?[a-zA-Z0-9\-\_]+;                                                                                                   { return protect(1, 0); }

/* File extension */
\.{Letter}+                                                                                                             { return protect(1, 0); }

/* Period in middle of a sentence */
\.{_}[:lowercase:]                                                                                                      { return protect(0, 2); }

/* Placeholders and separators */
\_\_+                                                                                                                   { return word(0, 0); }
\.\.+                                                                                                                   { return word(0, 0); }
\|\|+                                                                                                                   { return word(0, 0); }

/* Names with apostrophe, like "O'Malley" */
{_}[A-Z]'[A-Z]{Letter}+                                                                                                 { return protect(1, 0); }

/* Variable-like tokens such as ${dnt_1} */
[\$\%]\{[a-zA-Z0-9_]+\}                                                                                                 { return protect(1, 0); }

/**
 * String formatting variables (reference: https://developer.android.com/reference/java/util/Formatter)
 * %[argument_index$][flags][width][.precision]conversion
 */
\%([0-9]+\$)?([\-#+0,\(])?([0-9]+)?(\.[0-9]+)?([bBhHsScCdoxXeEfgGaA\%n]|[t|T][BbhAaCYyjmdeRTrDFc])[^a-zA-Z]             { return word(0, 1); }